allprojects {
    repositories {
        mavenCentral()
        maven {
            url = mavenRepoUrl
        }
    }
}

apply from: file('gradle/versioning.gradle')
apply from: file('gradle/convention.gradle')

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = "com.ctriposs.baiji"

    tasks.withType(Javadoc).each {
        it.classpath = sourceSets.main.compileClasspath
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    eclipse {
        classpath {
            // include 'provided' dependencies on the classpath
            plusConfigurations += configurations.provided
            downloadSources = true
            downloadJavadoc = true
        }
        jdt {
            sourceCompatibility = 1.7
            targetCompatibility = 1.7
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus += configurations.provided
        }
        project {
            languageLevel = 7.0
        }
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'
        testCompile 'org.slf4j:slf4j-simple:1.7.7'
        testCompile 'junit:junit:4.11'
    }
}

project(':baiji-core') {
    dependencies {
        compile 'org.codehaus.jackson:jackson-core-asl:1.9.13'
        compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
        compile 'com.google.guava:guava:17.0'
    }
}

project(':baiji-rpc-common') {
    sourceSets {
        main {
            java {
                srcDir 'generated_src'
            }
        }
    }

    dependencies {
        compile project(':baiji-core')
    }
}

project(':baiji-rpc-client') {
    dependencies {
        compile project(':baiji-core')
        compile project(':baiji-rpc-common')
        compile 'org.apache.httpcomponents:httpclient:4.3.5'
        compile 'com.ctriposs:etcd4j:1.0-SNAPSHOT'
    }
}

project(':baiji-rpc-server') {
    dependencies {
        compile project(':baiji-core')
        compile project(':baiji-rpc-common')
        compile 'io.netty:netty-all:4.0.4'
        compile 'javax.servlet:javax.servlet-api:3.0.1'
        compile 'org.apache.httpcomponents:httpcore:4.3.2'
        compile 'commons-lang:commons-lang:2.6'
        compile 'commons-beanutils:commons-beanutils:1.9.2'
        compile 'com.ctriposs:etcd4j:1.0-SNAPSHOT'
    }
}
